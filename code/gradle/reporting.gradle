/* PCGen code quality tasks. This file specifies the code quality
 * reports to be run as part of the PCGen nightly build. It is
 * called from the main build.gradle file.
 *
 * Usage: ./gradlew allReports
 */

checkstyle {
    configFile = new File('code/standards/checkstyle.xml')
    configProperties = [samedir: "${rootDir}/code/standards"]
    showViolations = true
    toolVersion = '10.7.0'
    sourceSets = []
}

pmd {
    // turn off the default rules
    ruleSets = []
    ruleSetFiles = files('code/standards/ruleset.xml')
    consoleOutput = false
    sourceSets = []
    toolVersion = "7.0.0"
    incrementalAnalysis = true
}

def classLoader = plugins['com.github.spotbugs'].class.classLoader
def SpotBugsConfidence = classLoader.findLoadedClass('com.github.spotbugs.snom.Confidence')

// TODO fix the outstanding bugs and then remove the ignoreFailures
spotbugs {
    toolVersion = '4.8.3'
    excludeFilter = file("${rootProject.projectDir}/code/standards/spotbugs_ignore.xml")
    omitVisitors = ['Naming', 'CrossSiteScripting', 'DontUseEnum', 'DoInsideDoPrivileged']
    // FJE: reportLevel = Confidence.LOW
    reportLevel = SpotBugsConfidence.LOW
    ignoreFailures = true
}

spotbugsMain {
    reports {
        xml.enabled = false
        html.enabled = true
        html.stylesheet resources.text.fromFile('code/standards/fancy-hist.xsl')
    }
}

task allReports {
    dependsOn = ['checkstyleMain', 'pmdMain', 'spotbugsMain']
}
